import os.path

Import('*')

# Add the install path so that gcc and newlib can find the tools that we are building
host_env = env_base.Clone()
host_env.AppendENVPath('PATH', Dir('install/bin').abspath)
# Set -jn in the environment instead of on the make command line so if it changes,
# scons won't try to rebuild everything (each target depends on the command used
# to build it).
host_env['ENV']['MAKEFLAGS'] = '-j%d' % GetOption('num_jobs')


target = 'arm-elf'

# Build packages
bootstrap_prefix = Dir('bootstrap').abspath
install_prefix = Dir('install').abspath
def BuildPackage(dir, isBootstrap, config_options, make_cmd):
	source_dir = Dir('#/firmware/external/%s' % dir)
	
	if isBootstrap:
		build_dir = Dir('build-boot/%s' % dir)
		prefix = bootstrap_prefix
	else:
		build_dir = Dir('build-install%s' % dir)
		prefix = install_prefix

	# These files will be created after complicated steps are completed successfully
	config_stamp = build_dir.File('.configured')
	build_stamp = build_dir.File('.built')

	# Configure in a build directory
	configure = host_env.Command(config_stamp, source_dir,
		'cd ${TARGET.dir} && ${SOURCE.abspath}/configure --prefix=%s %s && touch ${TARGET.file}' % (prefix, config_options))
	
	# Build the package
	build = host_env.Command(build_stamp, config_stamp, 'cd ${TARGET.dir} && %s && touch ${TARGET.file}' % make_cmd)
	Depends(build, configure)
	Clean(build, build_dir)
	
	return (configure, build)


(config_binutils_boot, build_binutils_boot) = BuildPackage('binutils', True,
	'--target=%s '
	'--enable-interwork '
	'--enable-multilib '
	'--with-gnu-ld '
	'--with-gnu-as '
	'--disable-werror'
	% target, 'make && make install')

(config_gcc_boot, build_gcc_compiler_boot) = BuildPackage('gcc', True,
	'--target=%s '
	'--without-headers '
	'--enable-languages="c" '
	'--disable-threads '
	'--disable-shared '
	'--with-gnu-as '
	'--with-gnu-ld '
	'--enable-interwork '
	'--with-system-zlib '
	'--enable-multilib ' %
	target, 'make all-gcc install-gcc')
Depends(config_gcc_boot, build_binutils_boot)

(config_newlib, build_newlib) = BuildPackage('newlib', False,
	'--target=%s '
	'--enable-interwork '
	'--with-gnu-ld '
	'--with-gnu-as '
	'--enable-newlib-hw-fp '
	'--enable-multilib'
	% target, 'make all install')
Depends(config_newlib, build_gcc_compiler_boot)
#FIXME: set env vars for make

(config_binutils, build_binutils) = BuildPackage('binutils', False,
	'--target=%s '
	'--enable-multilib '
	'--with-gnu-ld '
	'--with-gnu-as '
	'--disable-werror '
	'--enable-interwork '
	% target, 'make all install')
Depends(config_binutils, build_newlib)

(config_gcc, build_gcc) = BuildPackage('gcc', False,
	'--target=%s '
	'--with-gnu-as '
	'--with-gnu-ld '
	'--enable-languages=c '
	'--enable-multilib '
	'--enable-interwork '
	'--with-newlib '
	'--with-system-zlib '
	'--with-headers=%s '
	'--disable-werror '
	% (target, Dir('#/firmware/external/newlib/newlib/libc/include')), 'make all install')
Depends(config_gcc, build_binutils)


# Aliases to allow specific parts of the toolchain to be rebuilt/cleaned
Alias('arm-binutils', build_binutils)
Alias('arm-gcc', build_gcc)
Alias('arm-newlib', build_newlib)
Alias('arm-toolchain', ['arm-binutils', 'arm-gcc', 'arm-newlib'])

# Help('  arm-toolchain: ARM %s, %s, and %s\n' % ('arm-binutils', 'arm-gcc', 'arm-newlib'))

# To remove the installed files, you must clean all of the toolchain.
# The packages do not provide any way to uninstall files.
Clean('arm-toolchain', Dir('install'))

# ARM build environment
arm = host_env.Clone()
arm.Replace(CC='arm-elf-gcc')

arm.Append(BUILDERS={'Binary': Builder(
	action='arm-elf-objcopy -O binary $SOURCE $TARGET',
	suffix='.bin',
	src_suffix='.elf')})

# Add a dependency on the complete toolchain to every object
def ArmEmitter(target, source, env):
	for s in source:
		env.Depends(s, 'arm-toolchain')
	return (target, source)
if not GetOption('clean'):
	# Doing this all the time causes the toolchain to be cleaned whenever a target built with it is cleaned.
	#
	# We are subverting scons' automatic cleaning: build the toolchain when building targets that need it,
	# but only clean it if explicitly instructed to do so on the command line.
	arm.AppendUnique(PROGEMITTER=ArmEmitter)
	arm.AppendUnique(LIBEMITTER=ArmEmitter)

Export('arm')
